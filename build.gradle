/*
 * Copyright 2009 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy:1.7.5'
    compile 'org.apache.poi:poi-ooxml:3.7'
    testCompile 'junit:junit:4.+'
}

sourceSets {
    test.output.resourcesDir = 'build/classes/test'
}

def defaultEncoding = 'UTF-8'
compileJava {
    options.encoding = defaultEncoding
}
compileTestJava {
    options.encoding = defaultEncoding
}
compileGroovy {
    groovyOptions.encoding = defaultEncoding
    options.encoding = defaultEncoding
}
compileTestGroovy {
    groovyOptions.encoding = defaultEncoding
    options.encoding = defaultEncoding
}

uploadArchives {
    repositories.mavenDeployer {
        //
        // If you want to deploy it, set an entry which has a key of 'mavenDeployRepo'
        // e.g.) In $HOME/.gradle/gradle.properties:
        //     mavenDeployRepo  = file://localhost/<YOUR_MAVEN_REPO_FULL_PATH>
        //
        // If you want to choose some repo, set some entries which have keys started with 'mavenDeployRepo.'.
        // e.g.) In $HOME/.gradle/gradle.properties:
        //     mavenDeployRepo.release  = file://localhost/<YOUR_MAVEN_REPO_RELEASE_FULL_PATH>
        //     mavenDeployRepo.snapshot = file://localhost/<YOUR_MAVEN_REPO_SNAPSHOT_FULL_PATH>
        // And you execute gradle in command line:
        // e.g.)
        //     $ gradle uploadArchives -Dmaven.deploy.repo=snapshot
        //
        def mavenDeployRepoId = System.properties["maven.deploy.repo"]
        if (mavenDeployRepoId) {
            repository url: getProperty("mavenDeployRepo.${mavenDeployRepoId}")
        } else {
            repository url: mavenDeployRepo
        }

        // License
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}
